#!/usr/bin/env groovy
@Grab('se.alipsa.uso:uso-core:0.0.1')
@Grab('org.slf4j:slf4j-simple:2.0.17')
@GrabConfig(systemClassLoader=true)
import org.codehaus.groovy.jsr223.GroovyScriptEngineImpl
import se.alipsa.uso.ProjectBuilder
import se.alipsa.uso.TargetOnceProjectBuilder

ProjectBuilder projectBuilder = new TargetOnceProjectBuilder()
GroovyScriptEngineImpl scriptEngine = new GroovyScriptEngineImpl()

def parseArgs() {
  File buildFile
  List<String> targets = []
  List<String> props = []
  if (args.length < 1) {
    buildFile = new File("build.groovy")
  } else {
    buildFile = new File(args[0])
    if (!buildFile.exists()) {
      buildFile = new File("build.groovy")
    }
  }
  args.each {
    if (it != buildFile.name && !it.startsWith('-D')) {
      targets << it
    }
    if (it.startsWith('-D')) {
      props << it.substring(2)
    }
  }
  [
      buildFile: buildFile,
      targets: targets,
      systemProperties: props
  ]
}

def params = parseArgs()
if (!params.buildFile.exists()) {
  println "Build file ${params.buildFile.name} does not exist, specify it as the first parameter or name the build file build.groovy"
  System.exit(1)
}

params.systemProperties.each {
  def keyVal = it.split('=')
  System.setProperty(keyVal[0], keyVal[1])
}
scriptEngine.put("project", projectBuilder)
scriptEngine.eval(params.buildFile.text)

if (params.targets.contains('-p') || params.targets.contains('--projecthelp')) {
  println projectBuilder.targets.keySet()
  return
} else {
  projectBuilder.execute(params.targets)
}