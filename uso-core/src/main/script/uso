#!/usr/bin/env groovy
@Grab('se.alipsa.uso:uso-core:0.0.1')
@Grab('org.slf4j:slf4j-simple:2.0.17')
@GrabConfig(systemClassLoader=true)
import org.apache.tools.ant.BuildException
import org.codehaus.groovy.control.CompilationFailedException
import se.alipsa.uso.AntTargetBuilder
import se.alipsa.uso.ErrorReportingListener
import se.alipsa.uso.ProjectBuilder

ProjectBuilder projectBuilder = new AntTargetBuilder()
def binding = new Binding()
binding.setVariable("project", projectBuilder)
def shell = new GroovyShell(binding)


def parseArgs() {
  File buildFile
  List<String> targets = []
  List<String> props = []
  if (args.length < 1) {
    buildFile = new File("build.groovy")
  } else {
    buildFile = new File(args[0])
    if (!buildFile.exists()) {
      buildFile = new File("build.groovy")
    }
  }

  args.each {
    if (it != buildFile.name && !it.startsWith('-D')) {
      targets << it
    }
    if (it.startsWith('-D')) {
      props << it.substring(2)
    }
  }
  [
      buildFile: buildFile,
      targets: targets,
      systemProperties: props
  ]
}

def params = parseArgs()
params.systemProperties.each {
  def keyVal = it.split('=')
  System.setProperty(keyVal[0], keyVal[1])
}

projectBuilder.project.addBuildListener(new ErrorReportingListener(params.buildFile))
def codeSource = new GroovyCodeSource(params.buildFile.text, params.buildFile.name, params.buildFile.absoluteFile.getParent())
//def codeSource = new GroovyCodeSource(params.buildFile) // possible charset problem, since systemDefault is used
def compiledScript
// Eval phase
try {
  compiledScript = shell.parse(codeSource)
  compiledScript.run()
  // the script might have mutated or replaced the project binding
  projectBuilder = binding.getVariable("project") as ProjectBuilder
} catch (CompilationFailedException e) {
  println "Syntax error in build script ${params.buildFile.canonicalPath}:"
  println "  Message: ${e.message}"
  System.exit(1)
} catch (IOException e) {
  println "Error reading build script: ${e.toString()}"
  System.exit(1)
} catch (Exception e) {
  println "Error evaluating build script: ${e.toString()}"
  System.exit(1)
}

if (params.targets.contains('-p') || params.targets.contains('--projecthelp')) {
  println projectBuilder.targets.keySet()
  return
} else {
  if (projectBuilder.groupId == null) {
    projectBuilder.echo("No groupId set.")
  }
  if (projectBuilder.artifactId == null) {
    projectBuilder.echo("No artifactId set.")
  }
  if (projectBuilder.version == null) {
    projectBuilder.echo("No version set.")
  }
  // Execute phase
  try {
    projectBuilder.execute(params.targets)
  } catch (BuildException be) {
    // BuildException already handled by ErrorReportingListener
    System.exit(1)
  } catch (Exception e) {
    // Handle other exceptions that might not go through the BuildListener
    System.err.println "Fatal error when executing targets ${params.targets} in ${params.buildFile.canonicalPath}:"
    System.err.println " -> ${e.class.name}: ${e.message}"

    // Find build script related stack trace elements
    def trace = e.stackTrace.find { it.fileName?.contains(params.buildFile.name) }
    if (trace) {
      System.err.println " -> Error in ${trace.fileName}, line ${trace.lineNumber}"
    }

    System.exit(1)
  }
}