#!/usr/bin/env groovy
@Grab('org.apache.maven.resolver:maven-resolver-ant-tasks:1.6.0')
@Grab('org.apache.ant:ant-junitlauncher:1.10.15')
@Grab('org.codehaus.plexus:plexus-xml:4.1.0')
@Grab(group='org.slf4j', module='slf4j-simple', version='2.0.17', scope='test')
@GrabConfig(systemClassLoader=true)
import groovy.ant.AntBuilder

groupId = 'se.alipsa.uso'
artifactId = 'uso-tasks'
version = '0.1.0'
ant = new AntBuilder()

ant.typedef(name:"dependency", classname:"org.apache.maven.resolver.internal.ant.types.Dependency")
ant.typedef(name:"dependencies", classname:"org.apache.maven.resolver.internal.ant.types.Dependencies")
ant.taskdef(name:"resolve", classname:"org.apache.maven.resolver.internal.ant.tasks.Resolve")

def jaxbVersion = '4.0.5'
def xmlUnitVersion = '2.10.0'
ant.dependencies(id: 'deps') {
  dependency(coords: "org.glassfish.jaxb:jaxb-core:${jaxbVersion}")
  dependency(coords: "org.glassfish.jaxb:jaxb-runtime:${jaxbVersion}")
  dependency(coords: "org.apache.maven:maven-model:3.9.11")
  dependency(coords: "org.apache.maven.resolver:maven-resolver-api:1.9.24")
  dependency(coords: "org.apache.maven.resolver:maven-resolver-ant-tasks:1.6.0")
  dependency(coords: "org.apache.ant:ant-junitlauncher:1.10.15:test")
  dependency(coords: "org.xmlunit:xmlunit-core:${xmlUnitVersion}:test")
  dependency(coords: "org.xmlunit:xmlunit-matchers:${xmlUnitVersion}:test")
  dependency(coords: "com.squareup.okhttp3:mockwebserver:4.12.0:test")
}

groovyHome = System.getenv("GROOVY_HOME")

groovyClasspath = new File(groovyHome, "lib").listFiles().findAll { it.name.endsWith(".jar") }

createProjectLayout()

if (args.contains('clean')) {
  clean()
  createProjectLayout()
}
if (args.contains('compile')) {
  compile()
}

if (args.contains('test')) {
  compile()
  compileTest()
  test()
}

if (args.contains('jar')) {
  clean()
  createProjectLayout()
  compile()
  jar()
}

if (args.contains('publishLocal')) {
  clean()
  createProjectLayout()
  compile()
  compileTest()
  test()
  jar()
  createPom()
  publishLocal()
}

if (args.contains('deploy')) {
  deploy()
}

/**
 * Check if a target has already been run.
 * If it has, return true and set the variable to true.
 * If it hasn't, set the variable to false and return false.
 *
 * @param targetName the target name to check
 * @return true if the target has already been run, false otherwise
 */
boolean hasRan(String targetName) {
  boolean result = binding.hasVariable(targetName) && binding.getVariable(targetName)
  binding.setVariable(targetName, true)
  return result
}

def clean() {
  if (hasRan('isCleaned')) return
  println "\n[clean]"
  ant.delete dir: buildDir
  ant.mkdir dir: mainBuildDir
}

def compile() {
  if (hasRan('isCompiled')) return
  println "\n[compile]"
  ant.resolve(dependenciesref: 'deps') {
    path(refId: 'testPath', classpath: 'test')
  }
  ant.path(id: 'compilePath') {
    pathelement location: mainBuildDir
    fileset dir: testBuildDir
    fileset dir: "$groovyHome/lib", includes: '*.jar'
    path refId: 'testPath'
  }
  ant.taskdef(name: 'groovyc', classname: 'org.codehaus.groovy.ant.Groovyc')
  ant.echo "Compiling main groovy classes"
  ant.groovyc(
      srcdir: 'src/main/groovy',
      destdir: mainBuildDir,
      classpathref: 'compilePath'
  ) {
    javac {
      include(name: '**/*.java')
    }
  }
}

def compileTest() {
  if (hasRan('isCompiledTest')) return
  println "\n[compileTest]"
  ant.echo "Compiling test groovy classes"
  ant.groovyc(srcdir: 'src/test/groovy', destdir: testBuildDir) {
    classpath {
      pathelement path: mainBuildDir
      fileset dir: testBuildDir
      groovyClasspath.each { jar ->
        pathelement path: jar
      }
      path refId: 'testPath'
    }
    javac {
      include(name: '**/*.java')
    }
  }
}

def test() {
  if (hasRan('isTested')) return
  println "\n[test]"
  def outputDir = new File(buildDir, "testoutput")
  if (!outputDir.exists()) {
    outputDir.mkdirs()
  }


  ant.resolve(dependenciesref: 'deps') {
    ant.path(refId: 'testPath', classpath: 'test')
  }

  def testFailed = false

  try {
    ant.junitlauncher(haltOnFailure: true, printSummary: 'withOutAndErr') {
      classpath {
        pathelement location: mainBuildDir
        pathelement location: testBuildDir
        groovyClasspath.each { jar ->
          pathelement location: jar
        }
        path refid: "testPath"
      }
      testclasses(outputdir: outputDir) {
        fork(includeJUnitPlatformLibraries: false, includeAntRuntimeLibraries: false)
        fileset dir: testBuildDir
        listener type: "legacy-brief", sendSysOut: true
        // Critical: Use legacy-xml to capture full data
        listener type: "legacy-xml"
      }
    }
  } catch (e) {
    // Catch the build failure exception thrown by haltOnFailure: true
    testFailed = true
    println "JUnitLauncher failed, proceeding to generate detailed report..."
  } finally {
    // Create html report
    File reportDir = new File(outputDir, "html")
    reportDir.mkdirs()
    ant.junitreport(todir: reportDir) {
      fileset(dir:outputDir) {
        include name:"TEST-*.xml"
      }
      report format:"frames", todir: reportDir
    }
    def failedTests = []
    outputDir.eachFileMatch(~/TEST-.*\.xml/) { file ->
      def xml = new groovy.xml.XmlSlurper().parse(file)
      xml.testcase.each { tc ->
        tc.failure.each { f ->
          println "FAILED: ${tc.@classname}.${tc.@name}"
          println f.text().trim()
          println "-" * 80
          failedTests << tc
        }
        tc.error.each { e ->
          println "ERROR: ${tc.@classname}.${tc.@name}"
          println e.text().trim()
          println "-" * 80
          failedTests << tc
        }
      }
    }

    // 3. Re-throw the error to fail the build if tests actually failed
    if (testFailed) {
      // Use an Ant task to re-fail the build using the original message
      ant.fail(message: "${failedTests.size()} tests failed!. See detailed report above.")
    }
  }
}

def jar() {
  if (hasRan('isJared')) return
  println "\n[jar]"
  jarName = "$artifactId-${version}.jar"
  ant.mkdir dir: 'out/jar'
  ant.jar(destfile: "out/jar/$jarName", basedir: mainBuildDir)
}

def createPom() {
  if (hasRan('isCreatedPom')) return
  println "\n[createPom]"
  pomName = "$artifactId-${version}.pom"
  def pomFile = new File(mainBuildDir, pomName)
  pomFile.text =  """<?xml version="1.0" encoding="UTF-8"?>
  <project xmlns="http://maven.apache.org/POM/4.0.0"
    xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
    xsi:schemaLocation="http://maven.apache.org/POM/4.0.0 http://maven.apache.org/xsd/maven-4.0.0.xsd">
    <modelVersion>4.0.0</modelVersion>
    <groupId>$groupId</groupId>
    <artifactId>$artifactId</artifactId>
    <version>$version</version>
    <packaging>jar</packaging>
    <name>Uso Ant Tasks</name>
    <description>Ant taks for uso builds</description>
    <url>https://github.com/Alipsa/uso</url>
    <licenses>
      <license>
        <name>MIT License</name>
        <url>https://raw.githubusercontent.com/Alipsa/uso/refs/heads/main/LICENSE</url>
      </license>
    </licenses>
    <developers>
      <developer>
        <id>perNyfelt</id>
        <name>Per Nyfelt</name>
      </developer>
    </developers>
    <scm>
      <connection>scm:git:https://github.com/Alipsa/uso.git</connection>
      <developerConnection>scm:git:git@github.com:Alipsa/uso.git</developerConnection>
      <url>https://github.com/Alipsa/uso</url>
    </scm>
    <dependencies>
      <dependency>
        <groupId>org.apache.maven.resolver</groupId>
        <artifactId>maven-resolver-api</artifactId>
        <version>1.9.24</version>
      </dependency>
      <dependency>
        <groupId>org.apache.maven</groupId>
        <artifactId>maven-model</artifactId>
        <version>3.9.11</version>
      </dependency>
      <dependency>
        <groupId>org.apache.maven.resolver</groupId>
        <artifactId>maven-resolver-ant-tasks</artifactId>
        <version>1.6.0</version>
      </dependency>
      <dependency>
        <groupId>org.glassfish.jaxb</groupId>
        <artifactId>jaxb-core</artifactId>
        <version>4.0.5</version>
      </dependency>
      <dependency>
        <groupId>org.glassfish.jaxb</groupId>
        <artifactId>jaxb-runtime</artifactId>
        <version>4.0.5</version>
      </dependency>
    </dependencies>
  </project>
  """.stripIndent()
  ant.echo "created $pomFile"
}

def publishLocal() {
  if (hasRan('isPublishedLocal')) return
  println "\n[publishLocal]"
  def m2HomeDir = System.getProperty("M2_HOME") ?: System.getProperty("user.home") + "/.m2"
  def m2Home = new File(m2HomeDir)
  if (!m2Home.exists()) {
    ant.echo "Unable to locate the local maven repository. Please set the M2_HOME environment variable."
    return
  }
  def groupDir = groupId.replace('.', '/')
  def localRepo = new File(m2Home, "repository/$groupDir/$artifactId/$version")
  if (!localRepo.exists()) {
    localRepo.mkdirs()
  }
  ant.copy file: "out/jar/$jarName", toDir: localRepo
  ant.copy file: "out/main/$pomName", toDir: localRepo
  ant.delete dir: "${System.getProperty('user.home')}/.groovy/grapes/se.alipsa.uso"
  ant.echo "Deployed to local maven repository at $localRepo"
}

def signJar() {
  if (hasRan('isSigned')) return
  println "\n[signJar]"
  def jarFile = new File(mainBuildDir, jarName)
  def sigFile = new File(mainBuildDir, "$jarName.asc")
  ant.signjar(jar: jarFile, alias: 'uso', keystore: 'uso.jks', storepass: 'changeIt', sigfile: sigFile)
}

def deploy() {
  //https://stackoverflow.com/questions/61250554/deploying-jar-to-nexus-using-maven-ant-task
  if (hasRan('isDeployed')) return
  ant.taskdef(uri:"antlib:org.apache.maven.resolver.ant", resource:"org/apache/maven/resolver/ant/antlib.xml")
  /*
  <typedef name="authentication"       classname="org.apache.maven.resolver.internal.ant.types.Authentication"/>
  <typedef name="proxy"                classname="org.apache.maven.resolver.internal.ant.types.Proxy"/>
  <typedef name="mirror"               classname="org.apache.maven.resolver.internal.ant.types.Mirror"/>
  <typedef name="localrepo"            classname="org.apache.maven.resolver.internal.ant.types.LocalRepository"/>
  <typedef name="remoterepo"           classname="org.apache.maven.resolver.internal.ant.types.RemoteRepository"/>
  <typedef name="remoterepos"          classname="org.apache.maven.resolver.internal.ant.types.RemoteRepositories"/>
  <typedef name="dependency"           classname="org.apache.maven.resolver.internal.ant.types.Dependency"/>
  <typedef name="dependencies"         classname="org.apache.maven.resolver.internal.ant.types.Dependencies"/>
  <typedef name="artifact"             classname="org.apache.maven.resolver.internal.ant.types.Artifact"/>
  <typedef name="artifacts"            classname="org.apache.maven.resolver.internal.ant.types.Artifacts"/>
  <typedef name="settings"             classname="org.apache.maven.resolver.internal.ant.types.Settings"/>

  <taskdef name="resolve"              classname="org.apache.maven.resolver.internal.ant.tasks.Resolve"/>
  <taskdef name="install"              classname="org.apache.maven.resolver.internal.ant.tasks.Install"/>
  <taskdef name="deploy"               classname="org.apache.maven.resolver.internal.ant.tasks.Deploy"/>
  <taskdef name="pom"                  classname="org.apache.maven.resolver.internal.ant.types.Pom"/>
   */

}

def createProjectLayout() {
  println "\n[createProjectLayout]"

  def buildDir = new File("out")
  if (!buildDir.exists()) {
    buildDir.mkdirs()
  }
  binding.buildDir = buildDir

  def mainBuildDir = new File(buildDir, "main")
  if (!mainBuildDir.exists()) {
    mainBuildDir.mkdirs()
  }
  binding.mainBuildDir = mainBuildDir

  def testSrcDir = new File("src/test/groovy")
  if (!testSrcDir.exists()) {
    testSrcDir.mkdirs()
  }

  def testBuildDir = new File(buildDir, "testClasses")
  if (!testBuildDir.exists()) {
    testBuildDir.mkdirs()
  }
  binding.testBuildDir = testBuildDir
}